# The httpd service provides the web interface to dCache
# administration. It consists of the http service cell (a small
# embedded web service) and several utility cells that collect
# information from dCache.

# Required variables
####################################################################

onerror shutdown
check -strong port
check -strong dcache.paths.config
check -strong dcache.paths.etc
check -strong cell.name
check -strong httpd.static-content.scripts
check -strong httpd.static-content.docs
check -strong httpd.static-content.images
check -strong httpd.static-content.styles
check -strong httpd.static-content.index
check -strong httpd.static-content.plots
check -strong httpd.static-content.plots.subdir


# Default content for httpd service.
#
# Can be redefined in etc/httpd.conf or additional content can
# be defined in httpdSetupExtra.
####################################################################

#
#  The following block defines the context set_alias_statistics to be either
#  "set alias statistics directory ${statisticsLocation}" if
#  ${statisticsLocation} exists and is a directory, or an empty string otherwise
#
define env defineStatisticsAlias.exe endDefine
   set context set_alias_statistics "set alias statistics directory ${statisticsLocation}"
endDefine
define env defineEmptyStatisticsAlias.exe endDefine
   set context set_alias_statistics ""
endDefine
onerror continue
test -d ${statisticsLocation}
set env have_stats_loc ${rc}
onerror shutdown
exec env defineStatisticsAlias.exe -ifok=have_stats_loc
exec env defineEmptyStatisticsAlias.exe -ifnotok=have_stats_loc


#
#  The following block checks that the configured plot directory exists if
#  ${billingToDb} has value yes.  If the directory doesn't exist then
#  a message is logged (that includes instructions for fixing the problem)
#  and billingToDb is set to no, so disabling the billing-plotting machinery.
#

define env showChownCommand.exe endDefine
    say -level=esay "    chown ${dcache.effective.user} ${httpd.static-content.plots}"
endDefine

define env disableBillingHistoryDueToMissingDir.exe endDefine
    say -level=esay "Billing is configured to write information to a database, which"
    say -level=esay "allows the web interface to present graphs showing how dCache was"
    say -level=esay "used over time."
    say -level=esay ""
    say -level=esay "To do this, the directory ${httpd.static-content.plots} must exist"
    say -level=esay "and be writable by the user dCache will run as (${dcache.effective.user})"
    say -level=esay ""
    say -level=esay "To create this directory, do:"
    say -level=esay ""
    say -level=esay "    mkdir -p ${httpd.static-content.plots}"
    eval "${dcache.effective.user}" "root" ==
    exec env showChownCommand.exe -ifnotok
    say -level=esay ""
    say -level=esay "Until this happens, support for plotting data access will be disabled."
    set env billingToDb no
endDefine

define env checkBillingHistoryDir.exe endDefine
    test -d ${httpd.static-content.plots}
    exec env disableBillingHistoryDueToMissingDir.exe -ifnotok
endDefine

define env setDcacheUserEffectiveUser.exe endDefine
    set env dcache.effective.user ${dcache.user}
endDefine

set env dcache.effective.user ${user.name}
onerror continue
eval "${dcache.user}" "" ==
exec env setDcacheUserEffectiveUser.exe -ifnotok

eval ${billingToDb} yes ==
exec env checkBillingHistoryDir.exe -ifok
onerror shutdown


#
#  The following block either creates a static web-page or starts the
#  plotting infrastructure depending on the value of billingToDb.  This
#  is called from the cell initialisation (*Setup) context.
#

define context noBillingHistory.html endDefine
  <html>
    <head><title>Billing History</title></head>
    <body background="/images/bg.svg" text="#000000" link="#000000" vlink="#000000" alink="#000000">
      <h1>Billing History</h1>
      <p>
        dCache is not configured to store billing information in a database.  Without this, plots showing
        historical access information cannot be generated.
      </p>
    </body>
  </html>
endDefine


define context billingHistory-db-no.exe endDefine
   set alias billingHistory context noBillingHistory.html
endDefine


define context billingHistory-db-yes.exe endDefine
   set alias billingHistory class org.dcache.services.billing.html.HttpBillingHistoryEngine "billingHistory \
     -plotsDir=${httpd.static-content.plots} \
     -subDir=${httpd.static-content.plots.subdir} \
     -generatePlots=${generatePlots} \
     -plotsProperties=${billingPlotPropertiesFile} \
     -plotsTimeout=30 \
     -exportType=png \
     -exportExt=.png \
     -dbAccessContext=classpath:org/dcache/services/billing/plot/billinghistory.xml \
     -dbAccess=${billingInfoAccess} \
     -dbAccessProperties=${billingInfoAccessPropertiesFile} \
     -dbAccessMaxInsertsBeforeCommit=${billingMaxInsertsBeforeCommit} \
     -dbAccessMaxTimeBeforeCommit=${billingMaxTimeBeforeCommitInSecs} \
     -dbUrl=${billingDbUrl} \
     -dbDriver=${billingDbDriver} \
     -dbUser=${billingDbUser} \
     -dbPass=${billingDbPass} \
     -pgPass=${billingDbPgPassFileName}"
   set alias plots directory ${httpd.static-content.plots}
endDefine


define context ${cell.name}Setup endDefine
   set alias default context home.html
   set alias <home> file ${httpd.static-content.index} -onError=default
   set alias docs directory ${httpd.static-content.docs}
   set alias images directory ${httpd.static-content.images}
   set alias styles directory ${httpd.static-content.styles}
   set alias scripts directory ${httpd.static-content.scripts}
   set alias offline context offline.html
   set alias context context *
   set alias <default> context missing.html
   set alias robots.txt context robots.txt
   set alias cellInfo  context cellInfoTable.html  -onError=offline
   set alias usageInfo context poolUsageTable.html -onError=offline
   set alias queueInfo context poolQueueTable.html -onError=offline
   set alias poolInfo class diskCacheV111.poolManager.HttpPoolMgrEngineV3
   set alias billing class diskCacheV111.cells.HttpBillingEngine
   set alias flushManager class diskCacheV111.hsmControl.flush.HttpHsmFlushMgrEngineV1 mgr=hfc css=default
   set alias pools class diskCacheV111.services.web.PoolInfoObserverEngineV2 showPoolGroupUsage=true
   ${set_alias_statistics}
   set alias info class org.dcache.services.info.InfoHttpEngine
   exec context billingHistory-db-${billingToDb}.exe
   set update 60
endDefine

# Default navigation page
####################################################################

define context home.html endDefine
<html>
<head><title>Online dCache Home</title></head>
<body background="/images/bg.svg" text="#000000" link="#000000" vlink="#000000" alink="#000000">

<br><br><br>

<center>
<table border="1" width="40%">
<tr>
<td align="center" valign="middle" >
<br><br>
<img src="/images/eagle-main.gif"><br><br>

<table>
<tr>
<td>
<a href="/cellInfo"><h1>Cell Services</h1></a>
<a href="/usageInfo"><h1>Pool (Space) Usage</h1></a>
<a href="/queueInfo"><h1>Pool Request Queues</h1></a>
<a href="/poolInfo"><h1>Pools</h1></a>
<a href="/billing/"><h1>Actions Log</h1></a>
<a href="/billingHistory/"><h1>Billing Plots</h1></a>
<a href="/poolInfo/restoreHandler/*"><h1>Restore Queue</h1></a>
<a href="/poolInfo/restoreHandler/lazy"><h1>Lazy Restore Queue</h1></a>
</td></tr>
</table>
<br>

</td></tr>
</table>

</center>
<br><br><br>
<hr>
<!--
<address><a href="/system/">system</a></address>
-->
</body>
</html>
endDefine

# Page to show for missing content
####################################################################

define context missing.html endDefine
<html>
<head><title>No such page</title></head>
<body background="/images/bg.svg">
<h1>Page not found.</h1>
</body>
</html>
endDefine

# Page to show for offline components
####################################################################

define context offline.html endDefine
<html>
<head><title>dCache OFFLINE</title></head>
<body background="/images/bg.svg">
<center><img src="/images/eagle-main.gif"></center>
<p>
<center>
<table border=0 width="80%">
<tr>
<td align=right width="50%"><img src="/images/trudey.gif"></td>
<td align=left width="50%"><img src="/images/sorry.gif"></td>
</tr>
</table>
</center>
</body>
</html>
endDefine

# Our /robots.txt file.  This advertises which parts of the HTTP
# service indexing robots (web-crawlers) should index.  The particular
# configuration below disallows all indexing.  Details on how to
# configure robot.txt files are available from:
# http://www.robotstxt.org/robotstxt.html
####################################################################

define context robots.txt endDefine
User-agent: *
Disallow: /
endDefine

# Any of the above defaults can be redefined in etc/httpd.conf
####################################################################

onerror continue
test -f ${dcache.paths.etc}/httpd.conf
exec file:${dcache.paths.etc}/httpd.conf -ifok
onerror shutdown

# Transfer observer collects information about active transfers
####################################################################

define context TransferObserverSetup endDefine
  table define large 0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16
  table define small 6,8,4,5,9,10,11,15
endDefine

create diskCacheV111.cells.TransferObserverV1 TransferObserver \
              "default \
               -loginBroker=LoginBroker \
               -update=60"


# PoolInfoObserverV3 collects information about pool groups
####################################################################

onerror shutdown
create diskCacheV111.services.web.PoolInfoObserverV3 poolCollector \
               "-refresh-time=60 \
               "

# WebCollectorV3 collects information about cells and pools
####################################################################

onerror shutdown
create diskCacheV111.cells.WebCollectorV3 collector \
    "PnfsManager \
     PoolManager \
     gPlazma \
     -loginBroker=LoginBroker,srm-LoginBroker \
     -replyObject"

# The http service cell provides the web interface
####################################################################

onerror shutdown
create org.dcache.services.httpd.HttpServiceCell ${cell.name} \
       "!httpdSetupExtra ${port}"
