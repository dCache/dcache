%{
//
%}

%class Task
%package org.dcache.pool.migration
%access package
%import static diskCacheV111.util.CacheException.*

%start TASK::Queued
%map TASK
%%
Queued
{
        startWithoutLocations
                GettingLocations
                {
                }
        startWithLocations
                [ ctxt.hasMoreLocations() ]
                UpdatingExistingFile
                {
                }
        startWithLocations
                [ !ctxt.isMetaOnly() ]
                InitiatingCopy
                {
                }
        startWithLocations
                Failed
                {
                        failPermanently(FILE_NOT_IN_REPOSITORY, "File skipped because it has no existing replicas");
                }
}

GettingLocations
Entry
{
        queryLocations();
}
{
        query_timeout
                Failed
                {
                        fail(TIMEOUT, "PnfsManager failed (no response)");
                }
        query_noroute
                Failed
                {
                        fail(SERVICE_UNAVAILABLE, "PnfsManager failed (no route to cell)");
                }
        query_failure(rc: Integer, cause: Object)
                [ rc == FILE_NOT_FOUND ]
                Failed
                {
                        failPermanently(FILE_NOT_FOUND, "File does not exist, skipped");
                }
        query_failure(rc: Integer, cause: Object)
                Failed
                {
                        fail(rc, "PnfsManager failed (" + cause + ")");
                }
        cancel
                Cancelled
                {
                }
        query_success
                [ ctxt.hasMoreLocations() ]
                UpdatingExistingFile
                {
                }
        query_success
                [ !ctxt.isMetaOnly() ]
                InitiatingCopy
                {
                }
        query_success
                Failed
                {
                        failPermanently(FILE_NOT_IN_REPOSITORY, "File skipped because it has no existing replicas");
                }
}

// We got existing copies. Try to update one of them. Keep
// trying until we succeed or we tried them all.
UpdatingExistingFile
Entry
{
        updateExistingReplica();
}
{
        copy_timeout
                [ ctxt.hasMoreLocations() ]
                UpdatingExistingFile
                {
                }
        copy_timeout
                [ ctxt.isEager() && !ctxt.isMetaOnly() ]
                InitiatingCopy
                {
                }
        copy_timeout
                Failed
                {
                        fail(TIMEOUT, "Remote pool failed (no response)");
                }
        copy_noroute
                [ ctxt.hasMoreLocations() ]
                nil
                {
                        updateExistingReplica();
                }
        copy_noroute
                [ ctxt.isEager() && !ctxt.isMetaOnly() ]
                InitiatingCopy
                {
                }
        copy_noroute
                Failed
                {
                        fail(SERVICE_UNAVAILABLE, "Remote pool failed (no route to cell)");
                }
        copy_failure(rc: Integer, cause: Object)
                [ rc == LOCKED ]
                Failed
                {
                        fail(rc, "Replica is locked on target pool");
                }
        copy_failure(rc: Integer, cause: Object)
                [ ctxt.hasMoreLocations() ]
                nil
                {
                        updateExistingReplica();
                }
        copy_failure(rc: Integer, cause: Object)
                [ !ctxt.isMetaOnly() ]
                InitiatingCopy
                {
                }
        copy_failure(rc: Integer, cause: Object)
                Failed
                {
                        fail(rc, String.format("Transfer to %s failed (%s)", ctxt.getTarget(), cause));
                }
        copy_success
                Copying
                {
                }
        cancel
                Cancelling
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == FILE_NOT_FOUND ]
                Failed
                {
                        failPermanently(message.getReturnCode(),
                                        String.format("Transfer to %s failed (%s); will not be retried",
                                                      ctxt.getTarget(),
                                                      message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() != 0 ]
                Failed
                {
                        fail(message.getReturnCode(),
                             String.format("Transfer to %s failed (%s)",
                                           ctxt.getTarget(),
                                           message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() ]
                WaitingForCopyReplicaReply
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.getMustMovePins() ]
                MovingPin
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                Done
                {
                }

}

// Ask pool to copy the file.
InitiatingCopy
Entry
{
        initiateCopy();
}
{
        copy_success
                Copying
                {
                }
        copy_nopools
                Failed
                {
                        fail(NO_POOL_ONLINE, "No targets");
                }
        copy_noroute
                Failed
                {
                        fail(SERVICE_UNAVAILABLE,
                             String.format("Pool %s failed (no route to cell)",
                                           ctxt.getTarget()));
                }
        copy_failure(rc: Integer, cause: Object)
                Failed
                {
                        fail(rc,
                             String.format("Pool %s failed (%s)",
                                           ctxt.getTarget(), cause));
                }
        copy_timeout
                WaitingForCopyFinishedMessage
                {
                        // No reply, but message could have been
                        // received anyway, so try to cancel it.
                        cancelCopy();
                }
        cancel
                Cancelling
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == FILE_NOT_FOUND ]
                Failed
                {
                        failPermanently(message.getReturnCode(),
                                        String.format("Transfer to %s failed (%s); will not be retried",
                                                      ctxt.getTarget(),
                                                      message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() != 0 ]
                Failed
                {
                        fail(message.getReturnCode(),
                             String.format("Transfer to %s failed (%s)",
                                           ctxt.getTarget(),
                                           message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() ]
                WaitingForCopyReplicaReply
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.getMustMovePins() ]
                MovingPin
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                Done
                {
                }
}

// Need to create more copies and received PoolMigrationCopyFinishedMessage ahead of
// PoolMigrationCopyReplicaMessage reply. Wait for reply before proceeding.
WaitingForCopyReplicaReply
{
        copy_success
                [ ctxt.hasMoreLocations() ]
                UpdatingExistingFile
                {
                }
        copy_success
                [ !ctxt.isMetaOnly() ]
                InitiatingCopy
                {
                }
        copy_success
                Failed
                {
                        failPermanently(FILE_NOT_IN_REPOSITORY, "File skipped because it does not have enough existing replicas");
                }
        copy_nopools
                Failed
                {
                        fail(NO_POOL_ONLINE, "No targets");
                }
        copy_noroute
                Failed
                {
                        fail(SERVICE_UNAVAILABLE,
                             String.format("Pool %s failed (no route to cell)",
                                           ctxt.getTarget()));
                }
        copy_failure(rc: Integer, cause: Object)
                Failed
                {
                        fail(rc,
                             String.format("Pool %s failed (%s)",
                                           ctxt.getTarget(), cause));
                }
        copy_timeout
                Failed
                {
                        fail(TIMEOUT,
                             String.format("Pool %s failed (no response)",
                                           ctxt.getTarget()));
                }
        cancel
                Cancelled
                {
                }
}

// Pool accepted the task. Wait for it to finish and
// periodically send a ping to verify it is alive.
Copying
Entry
{
        startTimer(ctxt.getPingPeriod());
}
Exit
{
        stopTimer();
}
{
        timer
                Pinging
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == FILE_NOT_FOUND ]
                Failed
                {
                        failPermanently(message.getReturnCode(),
                                        String.format("Transfer to %s failed (%s); will not be retried",
                                                      ctxt.getTarget(),
                                                      message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() != 0 ]
                Failed
                {
                        fail(message.getReturnCode(),
                             String.format("Transfer to %s failed (%s)",
                                           ctxt.getTarget(),
                                           message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() && ctxt.hasMoreLocations() ]
                UpdatingExistingFile
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() && !ctxt.isMetaOnly() ]
                InitiatingCopy
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() ]
                Failed
                {
                        failPermanently(FILE_NOT_IN_REPOSITORY, "File skipped because it does not have enough existing replicas");
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.getMustMovePins() ]
                MovingPin
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                Done
                {
                }
        cancel
                Cancelling
                {
                }
}

// Send a ping.
Pinging
Entry
{
        ping();
}
{
        ping_success
                Copying
                {
                        // Task is alive
                }
        ping_failure(rc: Integer, cause: Object)
                WaitingForCopyFinishedMessage
                {
                        // Task is gone
                }
        ping_noroute
                NoResponse
                {
                        // We cannot talk to the pool
                }
        ping_timeout
                NoResponse
                {
                        // Pool does not respond.
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == FILE_NOT_FOUND ]
                Failed
                {
                        failPermanently(message.getReturnCode(),
                                        String.format("Transfer to %s failed (%s); will not be retried",
                                                      ctxt.getTarget(),
                                                      message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() != 0 ]
                Failed
                {
                        fail(message.getReturnCode(),
                             String.format("Transfer to %s failed (%s)",
                                           ctxt.getTarget(),
                                           message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() && ctxt.hasMoreLocations() ]
                UpdatingExistingFile
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() && !ctxt.isMetaOnly() ]
                InitiatingCopy
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() ]
                Failed
                {
                        failPermanently(FILE_NOT_IN_REPOSITORY, "File skipped because it does not have enough existing replicas");
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.getMustMovePins() ]
                MovingPin
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                Done
                {
                }
        cancel
                Cancelling
                {
                }
}

// Pool did not reply to ping. Let's give it some time
// and ping it again.
NoResponse
Entry
{
        startTimer(ctxt.getNoResponseTimeout());
}
Exit
{
        stopTimer();
}
{
        timer
                nil
                {
                        ping();
                }
        ping_success
                Copying
                {
                        // Task is alive
                }
        ping_failure(rc: Integer, cause: Object)
                WaitingForCopyFinishedMessage
                {
                        // Task is gone
                }
        ping_noroute
                Failed
                {
                        // We still cannot talk to the pool
                        fail(SERVICE_UNAVAILABLE,
                             String.format("Pool %s failed (no route to cell)",
                                           ctxt.getTarget()));
                }
        ping_timeout
                Failed
                {
                        // Message timed out again
                        fail(TIMEOUT,
                             String.format("Pool %s failed (no response)",
                                           ctxt.getTarget()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == FILE_NOT_FOUND ]
                Failed
                {
                        failPermanently(message.getReturnCode(),
                                        String.format("Transfer to %s failed (%s); will not be retried",
                                                      ctxt.getTarget(),
                                                      message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() != 0 ]
                Failed
                {
                        fail(message.getReturnCode(),
                             String.format("Transfer to %s failed (%s)",
                                           ctxt.getTarget(),
                                           message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() && ctxt.hasMoreLocations() ]
                UpdatingExistingFile
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() && !ctxt.isMetaOnly() ]
                InitiatingCopy
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() ]
                Failed
                {
                        failPermanently(FILE_NOT_IN_REPOSITORY, "File skipped because it does not have enough existing replicas");
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.getMustMovePins() ]
                MovingPin
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                Done
                {
                }
        cancel
                Cancelling
                {
                }
}

// Target pool no longer has the task. Let's wait for
// the CopyFinished message.
WaitingForCopyFinishedMessage
Entry
{
        startTimer(ctxt.getTaskDeadTimeout());
}
Exit
{
        stopTimer();
}
{
        timer
                Failed
                {
                        fail(TIMEOUT,
                             String.format("Pool %s failed (no response)",
                                           ctxt.getTarget()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == FILE_NOT_FOUND ]
                Failed
                {
                        failPermanently(message.getReturnCode(),
                                        String.format("Transfer to %s failed (%s); will not be retried",
                                                      ctxt.getTarget(),
                                                      message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() != 0 ]
                Failed
                {
                        fail(message.getReturnCode(),
                             String.format("Transfer to %s failed (%s)",
                                           ctxt.getTarget(),
                                           message.getErrorObject()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() && ctxt.hasMoreLocations() ]
                UpdatingExistingFile
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() && !ctxt.isMetaOnly() ]
                InitiatingCopy
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.needsMoreReplicas() ]
                Failed
                {
                        failPermanently(FILE_NOT_IN_REPOSITORY, "File skipped because it does not have enough existing replicas");
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ ctxt.getMustMovePins() ]
                MovingPin
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                Done
                {
                }
}

MovingPin
Entry
{
        movePin();
}
{
        move_success
                Done
                {
                }
        move_failure(rc: Integer, cause: Object)
                Failed
                {
                        fail(rc, "Pin manager failed (" + cause + ")");
                }
        move_noroute
                Failed
                {
                        fail(SERVICE_UNAVAILABLE, "Pin manager failed (no route to cell)");
                }
        move_timeout
                Failed
                {
                        fail(TIMEOUT, "Pin manager failed (timeout)");
                }
        cancel
                Failed
                {
                        fail(DEFAULT_ERROR_CODE,
                             String.format("Cancelling task (%s) failed (data migrated but pin movement still underway)",
                                           ctxt.getCancelReason()));
                }
}

// User requested to abort the task, but we already asked
// a pool to copy the file. Let's try to cancel the copy
// and wait for CopyFinished.
Cancelling
Entry
{
        startTimer(ctxt.getTaskDeadTimeout());
        cancelCopy();
}
Exit
{
        stopTimer();
}
{
        timer
                Failed
                {
                        fail(TIMEOUT,
                             String.format("Cancelling task (%s) failed (no response from %s)",
                                           ctxt.getCancelReason(), ctxt.getTarget()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() != 0 ]
                Cancelled
                {
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 && ctxt.getMustMovePins() ]
                Failed
                {
                        fail(DEFAULT_ERROR_CODE,
                             String.format("Cancelling task (%s) failed (data migrated but still pins to move)",
                                           ctxt.getCancelReason()));
                }
        messageArrived(message: PoolMigrationCopyFinishedMessage)
                [ message.getReturnCode() == 0 ]
                Done
                {
                }
        cancel_success
                nil
                {
                }
        cancel_failure(rc: Integer, cause: Object)
                nil
                {
                }
        cancel_noroute
                Failed
                {
                        fail(SERVICE_UNAVAILABLE,
                             String.format("Cancelling task (%s) failed (no route)",
                                           ctxt.getCancelReason()));
                }
        cancel_timeout
                nil
                {
                }
}

// Successfully cancelled task. We got confirmation that the
// transfer did not go through
Cancelled
Entry
{
        notifyCancelled();
}
{
}

// Something went wrong. In some cases the transfer could have
// succeeded, but we don't know.
Failed
{
}

// Transfer completed.
Done
Entry
{
        notifyCompleted();
}
{
}

// This will cause the state machine to always be input enabled
Default
{
        Default
                nil
                {
                }
}

%%
